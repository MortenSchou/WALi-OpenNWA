Import('ProgEnv')
Import('BinInstallDir')

env = ProgEnv.Clone()


def _ProgramTarget(env, command, function, namespace, source):
    if function == None:
        function = command
    assert 'CPPDEFINES' not in env
    obj = env.Object('nwa-%s' % command, source,
                     CPPDEFINES={'CONSTRUCTION_FUNCTION': function,
                                 'CONSTRUCTION_NAMESPACE' : namespace})
    prg = env.Program('nwa-%s.exe' % command, obj)
    return prg

def SingleProgramTarget(env, command, function=None, namespace='construct'):
    return _ProgramTarget(env, command, function, namespace, 'single-nwa-command.cpp')

def DoubleProgramTarget(env, command, function=None, namespace='construct'):
    return _ProgramTarget(env, command, function, namespace, 'double-nwa-command.cpp')


built = [
    env.Program('nwa-print-dot.exe', ['print-dot.cpp']),
    env.Program('nwa-is-empty.exe', ['is-empty.cpp']),
    env.Program('nwa-get-example.exe', ['get-example.cpp']),
    env.Program('nwa-is-member.exe', ['is-member.cpp']),
    env.Program('nwa-language-equals.exe', ['language-equals.cpp']),
    env.Program('nwa-language-subseteq.exe', ['language-subseteq.cpp']),

    DoubleProgramTarget(env, 'intersect'),
    DoubleProgramTarget(env, 'union', 'unionNWA'),
    DoubleProgramTarget(env, 'concat', 'concatenate'),

    SingleProgramTarget(env, 'star'),
    SingleProgramTarget(env, 'complement'),
    SingleProgramTarget(env, 'determinize'),
    SingleProgramTarget(env, 'reverse'),

    SingleProgramTarget(env, 'expand', 'expandNwa', 'experimental')
]

i = env.Install(BinInstallDir, built)

built = i
Return('built')

# Yo emacs!
# Local Variables:
#   mode: python
# End:
